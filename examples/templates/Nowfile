[Now Template System Example]

Show how the template system works.

[templates/html_base]
description "Basic HTML structure."

<html>
<head>
    <title>$page_title</title>
</head>
<body>
    % body %
    BODY
    % ---- %
</body>
</html>

[templates/htmls]
description "Display a list of files in HTML format."
extends html_base

<div class="files-list">
% body %
    <h2>Files in $directory</h2>
    <ul>
    % file %
        <li>$file</li>
    % --- %
    </ul>
% --- %
</div>

[procedures/render]

template htmls
    . (page_title = "Directory Listings Example")
    | as tpl

list "/home" "/opt"
    | foreach directory {
        obj $tpl : emit "body"
        list a b c d
            | foreach file {
                obj $tpl : emit "file"
            }
    }
obj $tpl
    : render
    | as rendered_template

obj $rendered_template
    : split "\n"
    : length | as line_count

obj $line_count
    : eq 22
    | assert "Rendered template should have 22 lines, not $line_count ."

obj $rendered_template
    : find "<h2>Files in /home</h2>"
    : neq -1
    | assert "There should be a h2 header for /home"

return $rendered_template

[commands/run]
description "Show how templates work."

render

[commands/render]
description "Output a rendered template."

render | print
