[Now Website]

[constants]
dist_dir "dist"

[Pages/index.html]
page_title "Home"
header "Why you absolutely should avoid Now"

[Pages/index.html/content]
- {
    type header
    format h2
    content "The syntax is inspired by Tcl!"
}
- "The author really liked to write in Tcl (he says that it's the only
language in which he can go back to a project after a year and continue
working as if it was day 2 -- see, we're not even talking about Now itself
and already concluded the author is stupid!), but was annoyed by some clunky
constructions using multiple nested command execution, like when a command
is used as argument to another command, especially noticeable when indexing
lists or dicts."
> So, instead of writing this:
- {
    type snippet
    format code
    content {
        > set lista [list a "This value gets printed" c d]
        > puts [lindex $lista 1]
        > # This value gets printed
    }
}
> In Now you could write this:
- {
    type snippet
    format code
    content {
        > set lista [list a "This value gets printed" c d]
        > print [o \$lista : get 1]
        > # This value gets printed
    }
}
- {
    type header
    format h3
    content "Yeah, but no. The syntax is actually crazy!"
}
- "I know you're looking into the above example and thinking: \"why?\".
Of course! Why try to improve a syntax that's awful from the beginning?
Why keep building over such a terrible foundation?"
- "Well, that's not even half of the problem. You could write that same
code in yet another manner. The first line, for instance, could be written
as that:"
- {
    type snippet
    format code
    content {
        > list a "This value gets printed" c d | as lista
    }
}
> Actually, there's even a preferred way of writing these "pipelines":
- {
    type snippet
    format code
    content {
        > list a "This value gets printed" c d
        >     | as lista
    }
}
- "The \"pipe\" (`|`) has a special meaning. Or not. It depends on how each
 \"command\" decides to interpret it."
> You can understand it better by looking into how `print` works:
- {
    type snippet
    format code
    content {
        > print "--> " [list 1 2 3]
        > # --> (1 , 2 , 3)
        > list  10 20 30 | print "--> "
        > # --> (10 , 20 , 30)
    }
}
- "You see? A pipe will basically make the result of the previous command
to be used as the last argument for the next one."
- {
    type header
    format h3
    content "And it gets worse"
}
- "Getting an item from a list was done using `:`, that is like a pipe, but
indicates that the next command is a <strong>method</strong> of the previous
command's result. So, in this case:"
- {
    type snippet
    format code
    content {
        > print [o \$lista : get 1]
    }
}
- "You're basically telling Now to get the <em>object</em> \"lista\",
call the method \"get\" with \"1\" as argument, then print the result
of that."
> But, as you already know, we could also write it as:
- {
    type snippet
    format code
    content {
        > o \$lista
        >     : get 1
        >     | print
    }
}
- {
    type header
    format h3
    content "But what is that \"o\"???"
}
- "I hear you. What is that `o`?"
- "You see, in Now there's only one strict format for doing the most basic
thing, that is <em>calling commands</em>, and that is:"
- {
    type snippet
    format code
    content {
        > command arguments?
    }
}
> So, this works fine:
- {
    type snippet
    format code
    content {
        > print
    }
}
> And also this:
- {
    type snippet
    format code
    content {
        > print $lista
    }
}
> But <strong>not this</strong>:
- {
    type snippet
    format code
    content {
        > \$lista | print
    }
}
- "Because `\$lista` is <strong>not</strong> a <em>command</em>. It's simply
a value."
- "So we need a command to <em>reference</em> a value. And this command
is <em>o</em>. It takes some arguments and returns exactly these same
arguments."
- {
    code {
        > o \$lista
        >     | print
        > # (1 , 2 , 3 , 4 , "etc")
    }
}
- {
    h3 "And there's yet another way to \"get\"..."
}
- {
    code {
        > print (\$lista . 1)
    }
}
- "You remember I just said there's \"only one way\" of doing things
in Now? Well, it seems that rule is already broken!"
- "Actually what happens is that the parentheses are a form of
<em>syntatic sugar</em>: what they do is to turn an prefix notation into
<strong>infix notation</strong>."
> For instance, in this case:
- {
    code {
        > print (1 + 2 + 3 + 4 + 5)
        > # 15
    }
}
> What is <em>actually</em> happening is:
- {
    code {
        > print [+ 1 2 3 4 5]
        > # 15
    }
}
- 'The "language" sacrifices being coherent in the name of being easier to the
developer. Preposterous!'
- "Now, if you think about it, this construction:"
- {
    code {
        > o (a , b , c , d)
    }
}
> Is actually this:
- {
    code {
        > o [, a b c d]
    }
}
- "So it means the comma is actually an alias to `list`?"
> Yes, it is.
- {
    h2 "It focus too much on pipelines"
}
- 'Instead of mimicking the battle-tested behavior of good old languages, Now
focuses on having <strong>powerful pipelines</strong>. The idea is to be able
to do most of what you need without having to break the flow of the data from
the first command to the last.'
> Again, preposterous
- 'So stopping your train of thought to set a new variable is kinda
considered an anti-pattern in Now.'
- {
    h3 "A normal person would write code like this"
}
- {
    code {
        > # Remove the current user from /etc/passwd
        > o \$env
        >     : get "USER"
        >     | as username
        > path "/etc/passwd"
        >     | as filepath
        > o \$filepath
        >     : read.lines
        >     | as lines
        > o \$lines
        >     | foreach line {
        >         if [o \$line : contains \$username] {
        >             skip
        >         } {
        >             print \$line
        >         }
        >     }
    }
}
- {
    h3 "But a seasoned Now programmer would write this, instead"
}
- {
    code {
        > scope "Remove the current user from /etc/passwd" {
        >     o \$env
        >         : get "USER"
        >         | as username
        >     path "/etc/passwd"
        >         : read.lines
        >         | filter {o : contains \$username : eq false}
        >         | {print}
        > }
    }
}
- {
    h3 "Explicit is better than implicit, amiright?"
}
- "Did you noticed there's this weird `| {print}` in the end of the above
program? That's an <strong>implicit foreach</strong>. And we all know that
\"explicit is better than implicit\". Because it is."
- 'But Now, instead of following this divine rule, tries to make the developer
life easy by avoiding to write this all the time:'
- {
    code {
        >     | foreach line {
        >         print $line
        >     }
    }
}
- "And you know what? There's even a <strong>implicit transform</strong>!"
- {
    code {
        > list  1 2 3
        >    | {o : mul 10}  # this thing
        >    | {print}
        > # 10
        > # 20
        > # 30
    }
}
- "But we won't bend to this sloppy way of thinking, of course we would write
this much more correct, more explicit!, version:"
- {
    code {
        > list 1 2 3
        >     | transform x {
        >         return (\$x * 10)
        >     }
        >     | foreach x {
        >         print \$x
        >     }
    }
}
> Ah, much better! Look how many characters we are typing, now.
- {
    h3 "Line-continuation is marked by a dot"
}
- "Again, instead of doing things the way everybody does, that is, escaping
the newline character using a <strong>\\</strong> in the end of one line, Now
let the end of the line alone and indicates that <strong>the next line</strong>
is, in fact, a continuation of the previous one."
> So, this:
- {
    code {
        > print alfa beta gama
    }
}
> Can also be written like this:
- {
    code {
        > print
        >     . alfa beta gama
    }
}
> Or even:
- {
    code {
        > print
        >     . alfa
        >     . beta
        >     . gama
    }
}
> Is this supposed to look like some sort of ASCII art???
> What a joke!
- {
    h3 "You can handle errors inside the pipeline"
}
- "We all love a good <strong>block</strong> of try/catch, right? But Now
decided it's too normal for it to implement and, instead, have this thing
called \"events\" that also serve to handle errors."
> Like this:
- {
    code {
        > http.get "http://example.com/teste"
        >     ! 500 {print "Maybe the server is down?" ; return $error}
        >     ! 404 {print "Shit, this page doesn't even exist!" ; return $error}
        >     ! * {print "Shit, some other error occurred..." ; return $error}
        >     | return
    }
}
- "It's not so catchy at first glance, but imagine you're doing it directly
in your shell, like:"
- {
    code {
        > \$ now :cmd 'http.get "https://example.com/teste" ! * {exit 1} | print'
    }
}
- {
    h2 "It's not a multi-purpose programming language"
}
- "If it's not supposed to solve every possible problem, how can it be
any useful?"
- {
    h2 "It's not even a programming language!"
}
- "The author is emphatic in calling it <em>a tool</em>..."
- {
    h3 "You don't write programs, you write documents"
}
- "This may be the worst part."
- 'Imagine that! Instead of writing a "script" or a "program",
you are supposed to write a <strong>document!</strong> With a title,
a description and... and... <strong>sections!</strong>'
> Just like this:
- {
    code {
        > [Example Document]
        > 
        > This document exemplifies some of the syntax
        > of a Now document.
        > 
        > [procedures/hello]
        > parameters {
        >     who {
        >         type string
        >     }
        > }
        > 
        > print "Hello, \$who!"
        > 
        > [commands/run]
        > parameters {
        >     who {
        >         type string
        >         default "world"
        >     }
        > }
        > 
        > hello \$who
    }
}
- "Like... you can't simply shove code into a file and call it a day. No,
you have to follow the proper form, just like some ascient COBOL code."
- {
    h3 "And what about these 'commands'?"
}
- "A Now document saved as `Nowfile` in the current directory will make
running `now` without any arguments drop this in your terminal:"
- {
    code {
        > \$ now
        > Now Website
        > 
        >  passwd ---------> Test the code for 'grepping -v' the /etc/passwd file
        >  build ----------> Build the Website.
    }
}
- "Now you see why Now forces you to write a title for your document..."
- "In order to actually do something, you call one of the available
<em>commands</em>, like `now build`."
- {
    h2 "It lives in a gray area between a shell and a real programming language"
}
- "Now definitely is not something you could use as a shell, since you have to
declare \"system_commands\" in order to call anything from the system it's
running on."
- {
    code {
        > [system_commands/ls]
        > command {
        >     - ls
        > }
        > 
    }
}
- "Isn't that awful? Now even checks if the command actually is available
before running your prog... <em>document</em>. It may be nice to
<strong>not</strong> have any <em>command not found</em> being found during
runtime, but if that's the price, I don't know, maybe it isn't worth paying."
- {
    type header
    format h2
    content "It's written in D -- and nobody uses D"
}
- "D is nice, but the language has no direction, no plans, right? You
probably didn't even heard that before, since nobody talks about D."
- "Now has almost <strong>8k LOC</strong> written in D. What a shameful
decision that was!"
- {
    type header
    format h2
    content "It barely has standard library"
}
- "Yeah, really. It has a whole lot of various commands, but nothing even
close to Java or C#."
- {
    type header
    format h2
    content "It has zero database connectivity baked in"
}
- "In order to use a database, one have to rely on a \"library\". And
libraries are not like shared-object libraries as normal people would
expect: in Now, a library is implemented using a form of IPC: it keeps
an external program running and send or receive calls to <em>procedures</em>
using the standard input and output to send messages serialized in JSON."
- "So, technically, it's kinda easy to write a library to any DBMS, but who
would do that?"
- {
    type header
    format h2
    content "It can't even work with YAML!"
}
- "Yeah, it was abandoned because the author consider that there's no hope
for YAML, that the format is terrible and overly complex. Now you have to
convert everything to JSON if you want to work with data that's originally
in YAML."
- {
    type header
    format h2
    content "It implements weird concepts like \"shells\" and \"commands\""
}
- "Imagine your program <strong>have</strong> to call a bash script. Of course
you're going to have it in a file somewhere in your project folder! But, no,
in Now you can write this same script virtually like any other section in the
document and call it a <em>script</em>, to be executed by some <em>shell</em>."
> Like this:
- {
    code {
        > [shells/bash/scripts/date]
        > parameters {
        >     format {
        >         type string
        >         default "%Y-%m-%d"
        >     }
        > }
        > 
        > date +\$format
        > 
        > [commands/run]
        > 
        > date "%Y"
        >     | collect
        >     : first
        >     | print "The current year is "
    }
}
- "You can even define many shells that will call other programs and then
write the \"scripts\" inside the document. Like, imagine having a document
where you can write Markdown and it's capable of rendering itself."
- "And that's a waste. A folder with a 'document.md' and a 'run.sh' will
have the same effect."
- {
    h2 "It's not Web-focused"
}
- "What's the use on <em>anything</em> that can't serve HTTP???"
- "Now <strong>is</strong> fully capable of serving SCGI, though, but
what's the point? SCGI probably stands for SlowCGI, right?, the exact
opposite of FastCGI! Who cares if it's simple? What we all need
is <strong>fast!</strong>"
- "Because every single Website we create absolutely must support 100k clients
per second. We all know that."
- {
    h2 "And all that is only the tip of the iceberg..."
}
- "You can even define many shells that will call other programs and then
write the \"scripts\" inside the document. Like, imagine having a document
where you can write Markdown and it's capable of rendering itself."
- "And that's a waste. A folder with a 'document.md' and a 'run.sh' will
have the same effect."
- {
    h2 "It's not Web-focused"
}
- "What's the use on <em>anything</em> that can't serve HTTP???"
- "Now <strong>is</strong> fully capable of serving SCGI, though, but
what's the point? SCGI probably stands for SlowCGI, right?, the exact
opposite of FastCGI! Who cares if it's simple? What we all need
is <strong>fast!</strong>"
- "Because every single Website we create absolutely must support 100k clients
per second. We all know that."
- "<hr/>"
- {
    h2 "Stay away"
}
- {
    type sequence
    format p
    content {
        - "My advice: stay away from"
        - {
            type link
            format link
            text "the project repository on Github"
            href "http://github.com/now-run/now"
        }
        - "."
    }
}

[# Nop]

> What follows is a sample Now document:
- {
    type reference
    format code
    reference {
        - "Sample"
        - body
    }
}

[templates/style]

@font-face {
    font-family: Fira Sans ExtraBold;
    src:url("fonts/FiraSans-ExtraBold.ttf");
}
@font-face {
    font-family: Fira Sans Heavy;
    src:url("fonts/FiraSans-Heavy.ttf");
}
@font-face {
    font-family: Fira Sans Light;
    src:url("fonts/FiraSans-Light.ttf");
}
@font-face {
    font-family: FiraCode Light;
    src:url("fonts/FiraCode-Light.ttf");
}
@media (min-width: 0em) {
  #body {
    /* Desktop, usually */
    padding-top: 5px;
    padding-left: 100px;
    padding-right: 100px;
    margin-left: 80px;
    margin-right: 80px;
    font-size: 1.4em;
    background-color: #eee9bf;
    border: 4px solid black;
  }
}
@media (min-width: 100em) {
  #body {
    /* Mobile, usually */
    padding-top: 10px;
    padding-left: 35px;
    padding-right: 35px;
    margin-left: 200px;
    margin-right: 250px;
    font-size: 1.8em;
    background-color: #eee9bf;
    border: 4px solid black;
  }

}
h1 {
  font-family: Fira Sans Heavy;
}
h2,h3 {
  font-family: Fira Sans ExtraBold;
}
p,li {
  font-family: Fira Sans Light;
}
pre {
  font-family: FiraCode Light;
  color: white;
  background-color: black;
  padding: 15px;
  border-radius: 5px;
  width: 90%;
  margin-left: 2.5%;
}

[templates/base]

<html>
  <head>
    <title>$[
        when [vars : contains "page_title"] {
            return "$page_title - Now"
        }
        default {
            return "Now"
        }
    ]</title>
    <link rel="stylesheet" href="style.css">
  </head>
  <body>
  <div id='body'>
    <h1>
        $header
        % subheader %
        <br/><small>$subheader</small>
        % --------- %
    </h1>
    <div id='content'>
        % content %
            placeholder for block body
        % ---- %
    </div>
  </div>
  </body>
</html>

[templates/page]
extends base

% content %
$content
% ------- %

[templates/header]

    <h${level}>$content</h${level}>

[templates/p]

    <p>
    % content %
    $content
    % ------- %
    </p>

[templates/h2]

    <h2>
    % content %
    $content
    % ------- %
    </h2>

[templates/h3]

    <h3>
    % content %
    $content
    % ------- %
    </h3>



[templates/code]

    <pre>
    % content %
$content
    % --- %
    </pre>

[templates/link]

    <a href="$href" target="_blank">$text</a>

[procedures/render_item]
parameters {
    content {
        type any
    }
}
returns string

o $content
    | switch {type}
    ! string {
        template p
            : emit content
            : render
            | list
            | return
    }
    ! list {
        o $content
            | {
                render_item
                : to.sequence
            }
            | collect
            | return
    }
    ! dict {
        scope "Prepare content" {
            o $content
                : keys
                | as keys
                : contains "type"
                : then {
                    set format ($content . format)
                    set type ($content . type)
                } {
                    set type none
                    o $keys
                        : first
                        | as format

                    o $content
                        : set (content = ($content . $format))
                }
        }

        template $format | as tpl

        switch {o $type}
            ! sequence {
                obj $content
                    : get content
                    | foreach sub_item {
                        print "sub_item=<$sub_item>"
                        type $sub_item
                            | as sub_item_type
                            : eq string
                            : then {
                                obj $tpl
                                    : emit content (content = $sub_item)
                            } {
                                print ">> type is $sub_item_type"
                                render_item $sub_item
                                    | foreach x {
                                        obj $tpl
                                            : emit content (content = $x)
                                    }
                            }
                    }

                obj $tpl
                    : render
                    | list
                    | return
            }
            ! reference {
                obj $content
                    : get reference
                    : to.sequence
                    | . $text
                    | as content
            }
            ! link {
                set text ($content . text)
                set href ($content . href)
            }
            ! * {
                o $content
                    : get content
                    | as content
                    | type
                    : eq list
                    : then {
                        o $content
                            : join "\n"
                            | as content
                    }
            }

            scope "Render" {
                obj $tpl : emit content : render | list | return
            }
    }

[procedures/render_page]
parameters {
    filename {
        type string
    }
    data {
        type dict
    }
    template {
        type string
        default "page"
    }
}

template $template [obj $data : pairs] | as tpl

scope "page content" {
    obj $data : get content : pairs | list | foreach pair {
        set item_data ($pair . 1)
        render_item $item_data | foreach content {
            obj $tpl : emit content
        }
    }
}

scope "page body" {
    # obj $tpl : emit content
    # obj $tpl : emit subheader [obj $data : pairs]
    path "$dist_dir/$filename" : write [obj $tpl : render]
    obj $tpl : render | print
}

[commands/build]
description "Build the Website."

path "$dist_dir" | as dist_dir
if (($dist_dir : exists) == false) {
    mkdir $dist_dir
}

path "resources" : glob "*" | foreach entry {
    obj $entry : basename | as filename
    # TODO: make the copy automatically recursive.
    # copy $entry [path "$dist_dir/$filename"]
}

scope "style" {
    template style | as tpl
    path "$dist_dir/style.css" : write [obj $tpl : render]
}

obj $text : get "Pages" : pairs | list | foreach pair {
    set filename ($pair . 0)
    set data ($pair . 1)
    render_page $filename $data
}

[commands/passwd]
description "Test the code for 'grepping -v' the /etc/passwd file"

scope "Remove the current user from /etc/passwd" {
    o $env
        : get "USER"
        | as username
    path "/etc/passwd"
        : read.lines
        | filter {o : contains $username : eq false}
        | {print}
}
